plugins {
    id "java"
    id "idea"
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    annotationProcessor group: 'com.google.dagger', name: 'dagger-compiler', version: '2.14.1'
    implementation group: 'com.google.dagger', name: 'dagger', version: '2.14.1'
    implementation group: 'commons-io', name: 'commons-io', version: '2.4'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.10'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'

    implementation project(':core-mend')
    implementation files('../core-mend/build/libs/core-mend.jar')

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.18.0'
    testImplementation group: 'org.mockito.kotlin', name: 'mockito-kotlin', version: '5.0.0'
}

version = '4.1.0'

compileKotlin.dependsOn(':core-mend:jar')

sourceCompatibility JavaVersion.VERSION_11
targetCompatibility JavaVersion.VERSION_11

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

jar {

    duplicatesStrategy = 'exclude'

    manifest {
        attributes(
            'Implementation-Title': 'MEND',
            'Implementation-Version': project.version,
            'Main-Class': 'co.samco.mend4.desktop.Main'
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    archiveFileName = 'MEND4.jar'
}
