plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'kotlin-kapt'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.2")

    def dagger_version = "2.46.1"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation 'commons-io:commons-io:2.4'
    implementation 'commons-codec:commons-codec:1.10'
    implementation 'org.apache.commons:commons-lang3:3.7'

    implementation project(':core-mend')

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
}

version = '5.0.0'

compileKotlin.dependsOn(':core-mend:jar')

sourceCompatibility JavaVersion.VERSION_11
targetCompatibility JavaVersion.VERSION_11

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "11"
    }
}

jar {
    duplicatesStrategy = 'exclude'

    //Have to exclude the signature files from bouncy castle or you
    // get a SecurityException: Invalid signature file digest for Manifest main attributes
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'

    manifest {
        attributes(
                'Implementation-Title': 'MEND',
                'Implementation-Version': project.version,
                'Main-Class': 'co.samco.mend4.desktop.MainKt'
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    archiveFileName = 'mend.jar'
}
